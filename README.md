# TenzoM1CAddin

Внешняя Native API компонента для работы с весами Тензо-М на платформе 1С:Предприятие 8.

Текущая версия собрана для следующих платформ: Windows 32 и 64 bit, Linux 32 и 64 bit.

Есть внешняя обработка [test.epf](https://github.com/WendyH/TenzoM1CAddin/raw/master/test.epf), для примера и тестирования внешней компоненты.
В папке Package отдельные dll, путь к которым можно указывать при прямом подключении внешней компоненты без её установки.

<details>
  <summary>Пример подключения внешней dll</summary>
 
```bsl
КомпонентаПодключена = ПодключитьВнешнююКомпоненту("TenzoM1CAddinWin64.dll", "Vesy", ТипВнешнейКомпоненты.Native);
КомпонентаВесов = Новый("AddIn.Vesy.TenzoM");
ПортОткрыт = КомпонентаВесов.Подключиться("COM4", 9600, 1);
Вес = КомпонентаВесов.ПолучитьВес() / 1000;
Сообщить("Порт открыт: "+ПортОткрыт+" Код ошибки: "+КомпонентаВесов.КодОшибки+" Ошибка: "+КомпонентаВесов.Ошибка);
```
</details>
<details>
  <summary>Пример подключения внешней компоненты из макета</summary>
 
```bsl
&НаКлиенте
Процедура ПодключитьВнешнююКомпонентуДляРаботыСВесами()

	АдресВнешнейКомпоненты = ПолучитьАдресВнешнейКомпоненты();
	ДопПараметры = Новый Структура("ЭтоПервыйЗапуск,АдресВнешнейКомпоненты", Истина, АдресВнешнейКомпоненты);
	ОбработчикПодключения = Новый ОписаниеОповещения("ОбработчикЗавершенияПодключенияКомпоненты", ЭтаФорма, ДопПараметры);
	НачатьПодключениеВнешнейКомпоненты(ОбработчикПодключения, АдресВнешнейКомпоненты, "Vesy", ТипВнешнейКомпоненты.Native);

КонецПроцедуры

// Помещает макет внешней компоненты во временное хранилище и возвращает адрес
&НаСервере
Функция ПолучитьАдресВнешнейКомпоненты()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("ВнешняяКомпонентаТензоМ"); 
	Возврат ПоместитьВоВременноеХранилище(Макет);
	
КонецФункции

// Выполняется после завершения асинхронной установки внешней компоненты
&НаКлиенте
Процедура ОбработчикЗавершенияУстановкиКомпоненты(Знач ДополнительныеПараметры) Экспорт

	ДопПараметры = Новый Структура("ЭтоПервыйЗапуск,АдресВнешнейКомпоненты", Ложь, ДополнительныеПараметры.АдресВнешнейКомпоненты);
	ОбработчикПодключения = Новый ОписаниеОповещения("ОбработчикЗавершенияПодключенияКомпоненты", ЭтаФорма, ДопПараметры);
	НачатьПодключениеВнешнейКомпоненты(ОбработчикПодключения, ДополнительныеПараметры.АдресВнешнейКомпоненты, "Vesy", ТипВнешнейКомпоненты.Native);

КонецПроцедуры

// Выполняется после подключения внешней компоненты
&НаКлиенте
Процедура ОбработчикЗавершенияПодключенияКомпоненты(Знач Результат, Знач ДополнительныеПараметры) Экспорт

	КомпонентаПодключена = Результат;
	
	Если КомпонентаПодключена Тогда
		
		Попытка
			КомпонентаВесов = Новый("AddIn.Vesy.TenzoM");
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
		
	Иначе
		
		// Если не удалось подключить, возможно, внешняя компонента ещё не устанавливалась на данном рабочем месте.
		Если ТипЗнч(ДополнительныеПараметры)=Тип("Структура") И ДополнительныеПараметры.Свойство("ЭтоПервыйЗапуск") И ДополнительныеПараметры.ЭтоПервыйЗапуск Тогда
			
			ОбработчикУстановки = Новый ОписаниеОповещения("ОбработчикЗавершенияУстановкиКомпоненты", ЭтаФорма, ДополнительныеПараметры);
			НачатьУстановкуВнешнейКомпоненты(ОбработчикУстановки, ДополнительныеПараметры.АдресВнешнейКомпоненты);
			
		КонецЕсли;
		
		Сообщить("Не удалось подключить компоненту из макета");
		
	КонецЕсли;
	
КонецПроцедуры
```
</details>

## Свойства:
#### `Протокол (Protocol)`
Число 0 - Протокол TenzoM или 1 - Протокол 6.43.
#### `ИмяВесов (Name)`
Строка - название весов
#### `АдресУстройства (Adr)`
Число от 0 до 159. Адрес устройства, как он прописан в настройках весов.

#### `СетевойРежим (NetMode)`
Булево. Если включено - работа будет вестись по TCP/IP вместо работы по COM-портам.
Весовой терминал должен иметь сетевой интерфейс.
#### `СетевойАдрес (IP)`
Строка с IP адресом весового терминала для работы по сети.
#### `СетевойПорт (NetPort)`
Число. Обычно это 4001. Для работы с весами по сети.

#### `Подключен (Connected)`
Булево. Только чтение. Флаг состояния подключения к весовому терминалу.

#### `Событие (Event)`
Булево. Флаг, сигнализирующий о том, что было событие нажатия клавиши на весовом терминале.
Только чтение. Значение флага обновляется при считывании веса (вызов функции `ПолучитьВес()`).
#### `ВторойДатчик (Scale2)`
Булево. Флаг, сигнализирующий о том, что используется второй вход тензодатчика на терминале
(если таковой имеется).

#### `ВесСтабилен (Calm)`
Булево. Только чтение. Флаг успокоения веса.
#### `Перегрузка (Overload)`
Булево. Только чтение. Флаг перегрузки. Действителен только при работе по протоколу TenzoM.

#### `ПосылатьНажатиеКлавиш (SendKeys)`
Булево - если установлен, то при считывании веса функцией `ПолучитьВес()` и возникновении флага `Событие`
будет вызвана функция `ПолучитьВведенныйКод()` и код клавиши будет отправлен на устройство ввода системы.
Будет сэмулировано нажатие кнопок на клавиатуре. По-сути, нумпад весового терминала превратится в нумпад клавиатуры.
Устройство должно поддерживать данный метод (например, ТВ-019).
#### `ПосылатьКодыНумпадКлавиш (NumpadKeys)`
Булево - если установлен, то при установленном свойстве `ПосылатьНажатиеКлавиш` коды нажатия клавиш будут значениями именно __цифровых клавиш нумпада__ клавиатуры.
Например, если этот флаг не установлен, то код клавиши "0" будет 0x30 (VK_0), если установлен - то код будет 0x60 (VK_NUMPAD0).
#### `ГенерироватьВнешнееСобытие (RiseExternalEvent)`
Булево - если установлен, то при считывании веса функцией `ПолучитьВес()` и возникновении флага `Событие`
будет вызвана функция `ПолучитьВведенныйКод()` и результат передан в обработчик формы 1С `ВнешнееСобытие(Источник, Событие, Данные)`,
со значениями: `Источник` = "TenzoM1CAddin", `Событие` = "EnteredCode", а в `Данные` будет код нажатой клавиши на весовом терминале.

#### `Ошибка (Error)`
Текст последней возникшей ошибки.
#### `КодОшибки (ErrorCode)`
Число. Код последней возникшей ошибки. Если ошибок небыло - равен 0. Код может быть как кодом системной ошибки, так и кодом ошибки самого устройства.

#### `РежимЭмуляции (Emulate)`
Булево. Флаг, если установлен в Истина - Функция `Подключиться` всегда будет возвращать Истина.
А функция `ПолучитьВес` будет возвращать случайные величины веса.
Эмулируется заезд на монорельсовые весы подвешенного груза с последующим успокоением веса. 
Удобно для разработки и тестирования всяких внешних обработок 1С без подключения к реальным весам. 
#### `ЭмуляцияКилограммМинимально (EmulMinKg)`
Число - минимальное значение случайного веса в режиме эмуляции. По-умолчанию - 9.
#### `ЭмуляцияКилограммМаксимально (EmulMaxKg)`
Число - максимальное значение случайного веса в режиме эмуляции. По-умолчанию - 80.

#### `Логирование (WriteLog)`
Булево - если установлен флаг, то переданные и полученные данные от устройства будут писаться в лог файл, путь до
которого прописан в свойстве `ЛогФайл`.
#### `ЛогФайл (LogFile)`
Строка имени файла (с полным путём), в который будут писаться отправленные и полученные данные при включенном свойстве `Логирование`.

#### `РазделительДробнойЧасти (DecimalPoint)`
Символ запятой или точки - символ разделения дробной части, принятой в текущих настройках системы. По-умолчанию устанавливается
из настроек системы. Если при получении дробных значений веса функция `ПолучитьВес()` всегда возвращает целое число килограммов,
тогда попробуйте изменить этот символ разделителя дробной части.

## Методы:
### `Версия() / Version`
Возвращает строку со значением версии компоненты.

### `Подключиться(ИмяКомПорта, СкоростьОбмена, АдресУстройства) / Connect`
Где:
 - `ИмяКомПорта` - строка. Для Windows это строка вида "COM4", для Linux и Mac вида "tty4".
 - `СкоростьОбмена` - число. Как прописано в настройках весов. 9600/19200/57600/115200.
 - `АдресУстройства` - число от 1 до 159. Как прописано в настройках весов.
Возвращает Истина, если COM-порт был успешно открыт, т.е. к нему удалось подключиться.

Если свойство `СетевойРежим` включено, то `ИмяКомПорта` не имеет значения, а возврат Истина
будет возвращено, если к устройству удалось подключиться по адресу указанному в свойстве `СетевойАдрес`
и порту, указанному в свойстве `СетевойПорт`.

Если возвращаемое значение было Ложь - можно проверить свойство `КодОшибки` или сообщение в свойстве `Ошибка`.

<details>

  <summary>Пример:</summary>

```bsl
КомпонентаВесов.Протокол = 0; // 0 - Протокол TenzoM; 1 - протокол 6.43
	
ПортОткрыт = КомпонентаВесов.Подключиться("COM4", 9600, 1);

Если НЕ ПортОткрыт Тогда
    Сообщить("Код ошибки: "+КомпонентаВесов.КодОшибки+" Ошибка: "+КомпонентаВесов.Ошибка);
КонецЕсли;
```
</details>

### `Отключиться() / Disconnect`
Отключается от COM-порта или закрывает TCP/IP сокет.

### `ПолучитьВес() / GetWeight`
Получает вес в граммах. Т.е. для килограмм нужно делить на 1000.
<details>

  <summary>Пример:</summary>

```bsl
&НаКлиенте
Процедура ОбновитьВес() Экспорт

	Если НЕ ПортОткрыт Тогда Возврат; КонецЕсли;
	
	Вес = КомпонентаВесов.ПолучитьВес() / 1000;
	
	Если КомпонентаВесов.КодОшибки<>0 Тогда
		УстановитьСтрокуСостоянияПодключения("Ошибка "+КомпонентаВесов.КодОшибки+": "+КомпонентаВесов.Ошибка, WebЦвета.Красный);
	КонецЕсли;
		
	Элементы.НадписьЗначениеВеса.Заголовок  = Формат(Вес, "ЧДЦ=3;  ЧН=0,000");
	Элементы.НадписьЗначениеВеса.ЦветТекста = ?(КомпонентаВесов.ВесСтабилен, WebЦвета.Зеленый, WebЦвета.Красный);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтрокуСостоянияПодключения(СтрокаСостояния, Цвет)
	
	Элементы.НадписьСостоянияПодключения.Заголовок  = СтрокаСостояния;
	Элементы.НадписьСостоянияПодключения.ЦветТекста = Цвет;
	
КонецПроцедуры
```
</details>

### `ПереключитьВРежимВзвешивания() / SwitchToWeighing`
Переключает весы в режим взвешивания, если они были не в нём. Если был вывод текста на индикатор или вызвано
меню - будет переключён в рабочий режим с индикацией текущего веса.

### `ОбнулитьПоказанияВеса() / SetZero`
Устанавливает текущее показание веса в 0. То же самое, что нажатие клавиши >T< на весовом терминале.

### `ПолучитьВведенныйКод() / GetGetEnteredCode`
Возвращает символ нажатой клавиши на весовом терминале. Имеет смысл вызывать только, если свойство `Событие` включено.
Операция должна поддерживаться устройством.

### `ПолучитьТекстИндикатора(КодНомераСтроки) / GetIndicatorText`
Возвращает текст, отображаемый на индикаторе весового терминала. Работает только по протоколу TenzoM.

Где `КодНомераСтроки` число:
* 0 - весь текст целиком;
* 1 - верхняя строка (при двухстрочном индикаторе);
* 2 - нижняя строка (при двухстрочном индикаторе).

### `УстановитьТекстИндикатора(КодНомераСтроки, Текст) / SetIndicatorText`
Работает только по протоколу TenzoM.

Где `КодНомераСтроки` число:
* 0 - весь текст целиком;
* 1 - верхняя строка (при двухстрочном индикаторе);
* 2 - нижняя строка (при двухстрочном индикаторе).
`Текст` строка, выводимая на индикатор весового терминала:

### `УстановитьВходнойКанал(НомераКанала) / SetInputChannel`
Если весы оборудованы несколькими входными каналами для тензодатчиков - будет переключён на заданный канал.
Работает только по протоколу TenzoM. Устройство должно поддерживать данную операцию. 

### `СерийныйНомер() / GetSerialNum`
Возвращает число - серийный номер, котрый указан сзади устройства.
Работает только по протоколу TenzoM.

### `ИнформацияОбУстройстве() / GetDeviceInfo`
Возвращает строку - тип весов и версию прошивки.
Работает только по протоколу TenzoM.

### `ПолучитьIP(ВсеИнтерфейсы) / GetIP`
Возвращает строку с IP адресом клиента, на котором запущена компонента.
Параметр `ВсеИнтерфейсы` - тип Булево. Если Истина - вернёт несколько строк со всеми IP адресами всех устройств, даже виртуальных.

### `ПолучитьСтатус() / GetStatus`
Возвращает число, содержащее побитовый набор флагов, показывающих состояние весового терминала.
Работает только по протоколу TenzoM. Устройство должно поддерживать данную команду (код операции 0xBF).

STATUS – Байт состояния системы.
В зависимости от модификации прибора может иметь разное назначение. Оговаривается особо.
<details>

  <summary>Для дозаторов</summary>

```
D7 - =1 перезапуск прибора, сбрасывается после запроса счетчика перезапусков.
D6 - =1 есть состояние ошибки (тип ошибки по отдельной команде)
D5 - =1 режим НЕТТО, =0- режим БРУТТО
D4 - =1 признак нажатой , но не считанной клавиши.
D3 - =1 конец дозирования
D2 - =1 есть фиксация веса по входному сигналу
D1 - =1 выполняется калибровка АЦП в данный момент
D0 - =1 идет дозирование (набор веса) 
```
</details>
Если считывание статуса прошло успешно, обновляются значения свойств `Событие` и `ВесСтабилен`.

## Пример подключения компоненты:
```bsl
&НаСервере
Функция ПолучитьАдресВнешнейКомпонентыИзМакетаВнешнейОбработки()
	Обработка = РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("ВнешняяКомпонентаТензоМ"); 
	Адрес = ПоместитьВоВременноеХранилище(Макет);
	Возврат Адрес;
КонецФункции

&НаКлиенте
Функция ПодключитьВнешнююКомпонентуДляРаботыСВесамиНаКлиенте()
	
	КомпонентаПодключена = Ложь;
	
  АдресВнешнейКомпоненты = ПолучитьАдресВнешнейКомпонентыИзМакетаВнешнейОбработки();

  ОбработчикУстановки = Новый ОписаниеОповещения("ОбработчикЗавершенияУстановкиКомпоненты", ЭтаФорма);
  НачатьУстановкуВнешнейКомпоненты(ОбработчикУстановки, АдресВнешнейКомпоненты);
	
  Возврат КомпонентаПодключена;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикЗавершенияУстановкиКомпоненты(Знач ДополнительныеПараметры) Экспорт

  ОбработчикПодключения = Новый ОписаниеОповещения("ОбработчикЗавершенияПодключенияКомпоненты", ЭтаФорма);
  НачатьПодключениеВнешнейКомпоненты(ОбработчикПодключения, АдресВнешнейКомпоненты, "Vesy", ТипВнешнейКомпоненты.Native);
//НачатьПодключениеВнешнейКомпоненты(ОбработчикПодключения, "ОбщийМакет.TenzoM", "Vesy", ТипВнешнейКомпоненты.Native);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗавершенияПодключенияКомпоненты(Знач Результат, Знач ДополнительныеПараметры) Экспорт

	КомпонентаПодключена = Результат;
	
	Если КомпонентаПодключена Тогда
		Попытка
			КомпонентаВесов = Новый("AddIn.Vesy.TenzoM");
			ПодключитьсяКВесам();
		Исключение
			УстановитьСтрокуСостоянияПодключения(ОписаниеОшибки(), WebЦвета.Красный);
		КонецПопытки;
	Иначе
		УстановитьСтрокуСостоянияПодключения("Не удалось подключить компоненту из макета", WebЦвета.Красный);
	КонецЕсли;
	
КонецПроцедуры
```

