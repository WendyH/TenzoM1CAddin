# TenzoM1CAddin

Внешняя Native API компонента для работы с весами Тензо-М на платформе 1С:Предприятие 8. Написана на C++.

Текущая версия собрана для следующих платформ:
- Windows 32bit    
- Windows 64bit    
- Linux 32bit    
- Linux 64bit    
- MacOS 64bit (пока не работает)    

Есть внешняя обработка [test.epf](https://github.com/WendyH/TenzoM1CAddin/raw/master/test.epf), для примера и тестирования внешней компоненты.
В пвпке Package отдеьные dll, путь к которым можно уазывать при прямом подключении внешней компоненты без её установки.

```bsl
КомпонентаПодключена = ПодключитьВнешнююКомпоненту("TenzoM1CAddinWin64.dll", "Vesy", ТипВнешнейКомпоненты.Native);
КомпонентаВесов = Новый("AddIn.Vesy.TenzoM");
ПортОткрыт = КомпонентаВесов.Подключиться("COM4", 9600, 1);
Вес = КомпонентаВесов.ПолучитьВес() / 1000;
Сообщить("Порт открыт: "+ПортОткрыт+" Код ошибки: "+КомпонентаВесов.КодОшибки+" Ошибка: "+КомпонентаВесов.Ошибка);
```

## Свойства:
### Протокол / Protocol
Число от 1 до 4.
* 1 - Протокол TenzoM;
* 2 - Протокол 6.43;
* 3 - Работа по сети. Обращается к серверу, где работает Controller Net от компании Тензо-М.
* 4 - Работа по http. Обращается к серверу, где работает Controller Web от компании Тензо-М.
### СетевойАдрес / IP
Строка с IP адресом сервера для работы по сети или http с установленным Controller Net или Controller Web. 
### СетевойПорт / NetPort
Число. Обычно это 5001. Для работы с Controller Net.
### ВебПорт / WebPort
Число. Обычно это 5002 или 8080. Для работы с Controller Web.
### Подключен / Connected
Булево. Только чтение. Флаг состояния подключения к COM-порту.
### АдресУстройства / Adr
Число от 1 до 253. Адрес устройства, как он прописан настройках весов.
### ВесСтабилен / Calm
Булево. Только чтение. Флаг успокоения веса.
### Перегрузка / Overload
Булево. Только чтение. Флаг перегрузки. Действителен только при работе по протоколу TenzoM.
### Ошибка / Error
Текст последней возникшей ошибки.
### КодОшибки / ErrorCode
Число. Код последней возникшей ошибки. Если ошибок небыло - равен 0.
### РежимЭмуляции / Emulate
Булево. Флаг, если установлен в Истина - Функция `Подключиться` всегда будет возвращать Истина.
А функция `ПолучитьВес` будет возвращать случайные величины веса.
Эмулируется заезд на монорельсовые весы подвешенного груза с последующим успокоением веса. 
Удобно для разработки и тестирования всяких внешних обработок 1С без подключения к реальным весам. 

## Методы:
### Подключиться / Connect (ИмяКомПорта, СкоростьОбмена, АдресУстройства)
Где:
 - `ИмяКомПорта` - строка. Для Windows это строка "COM1", для Linux и Mac вида "tty5".
 - `СкоростьОбмена` - число. Как прописано в настройках весов. 9600/19200/57600/115200.
 - `АдресУстройства` - число от 1 до 253. Как прописано в настройках весов.
Возвращает Истина, если COM-порт был успешно открыт, т.е. к нему удалось подключиться.

Если возвращаемое значение было Ложь - можно проверить свойство `КодОшибки` или сообщение в свойстве `Ошибка`.

Пример:
```bsl
КомпонентаВесов.Протокол = 1; // 0 - Протокол TenzoM; 1 - протокол 6.43
	
ПортОткрыт = КомпонентаВесов.Подключиться("COM4", 9600, 1);

Если НЕ ПортОткрыт Тогда
    Сообщить("Код ошибки: "+КомпонентаВесов.КодОшибки+" Ошибка: "+КомпонентаВесов.Ошибка);
КонецЕсли;
```
### Отключиться / Disconnect ()
Отключается от COM-порта.
### ПолучитьВес / GetWeight ()
Получает вес в граммах. Т.е. для килограмм нужно делить на 1000.
Пример:
```bsl
&НаКлиенте
Процедура ОбновитьВес() Экспорт

	Если НЕ ПортОткрыт Тогда Возврат; КонецЕсли;
	
	Вес = КомпонентаВесов.ПолучитьВес() / 1000;
	
	Если КомпонентаВесов.КодОшибки<>0 Тогда
		УстановитьСтрокуСостоянияПодключения("Ошибка "+КомпонентаВесов.КодОшибки+": "+КомпонентаВесов.Ошибка, WebЦвета.Красный);
	КонецЕсли;
		
	Элементы.НадписьЗначениеВеса.Заголовок  = Формат(Вес, "ЧДЦ=3;  ЧН=0,000");
	Элементы.НадписьЗначениеВеса.ЦветТекста = ?(КомпонентаВесов.ВесСтабилен, WebЦвета.Зеленый, WebЦвета.Красный);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтрокуСостоянияПодключения(СтрокаСостояния, Цвет)
	
	Элементы.НадписьСостоянияПодключения.Заголовок  = СтрокаСостояния;
	Элементы.НадписьСостоянияПодключения.ЦветТекста = Цвет;
	
КонецПроцедуры
```

### ПереключитьВРежимВзвешивания / SwitchToWeighing ()
Переключает весы в режим взвешивания, если они были не в нём.
### ПолучитьДоступныеПорты / GetPorts ()
Список COM-портов через точку с запятой, доступные для подключения.
Пока не реализовано.
### Версия / Version ()
Возвращает строку со значением версии компоненты.
### ОбнулитьПоказанияВеса / SetZero ()
Ставит вес в 0.
### ПолучитьСтатус / GetStatus ()
Возвращает набор флагов, показывающих состояние весового терминала. Пока не реализовано.

## Пример подключения компоненты:
```bsl
&НаСервере
Функция ПолучитьАдресВнешнейКомпонентыИзМакетаВнешнейОбработки()
	Обработка = РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("ВнешняяКомпонентаТензоМ"); 
	Адрес = ПоместитьВоВременноеХранилище(Макет);
	Возврат Адрес;
КонецФункции

&НаКлиенте
Функция ПодключитьВнешнююКомпонентуДляРаботыСВесамиНаКлиенте()
	
	КомпонентаПодключена = Ложь;
	
  АдресВнешнейКомпоненты = ПолучитьАдресВнешнейКомпонентыИзМакетаВнешнейОбработки();

  ОбработчикУстановки = Новый ОписаниеОповещения("ОбработчикЗавершенияУстановкиКомпоненты", ЭтаФорма);
  НачатьУстановкуВнешнейКомпоненты(ОбработчикУстановки, АдресВнешнейКомпоненты);
	
  Возврат КомпонентаПодключена;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикЗавершенияУстановкиКомпоненты(Знач ДополнительныеПараметры) Экспорт

  ОбработчикПодключения = Новый ОписаниеОповещения("ОбработчикЗавершенияПодключенияКомпоненты", ЭтаФорма);
  НачатьПодключениеВнешнейКомпоненты(ОбработчикПодключения, АдресВнешнейКомпоненты, "Vesy", ТипВнешнейКомпоненты.Native);
//НачатьПодключениеВнешнейКомпоненты(ОбработчикПодключения, "ОбщийМакет.TenzoM", "Vesy", ТипВнешнейКомпоненты.Native);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗавершенияПодключенияКомпоненты(Знач Результат, Знач ДополнительныеПараметры) Экспорт

	КомпонентаПодключена = Результат;
	
	Если КомпонентаПодключена Тогда
		Попытка
			КомпонентаВесов = Новый("AddIn.Vesy.TenzoM");
			ПодключитьсяКВесам();
		Исключение
			УстановитьСтрокуСостоянияПодключения(ОписаниеОшибки(), WebЦвета.Красный);
		КонецПопытки;
	Иначе
		УстановитьСтрокуСостоянияПодключения("Не удалось подключить компоненту из макета", WebЦвета.Красный);
	КонецЕсли;
	
КонецПроцедуры
```

